#!/usr/bin/perl -w

#
# mails a welcome or update note to everyone in targets.adr
# $Id: aid_welcome,v 3.11 1998/05/26 18:51:12 mradwin Exp mradwin $
#

require 'aid_util.pl';
require 'getopts.pl';
require 'ctime.pl';

$pname = 'aid_welcome';
$usage = "usage: $pname [options] targets.adr
    -h            Display usage information.
    -d <secs>     Sleep for <secs> seconds between sending email
                  (default 10 for -m, 0 for all others).
    -r <reqtype>  Request type (0, 1 or 2) (only valid with -m and -c).
    -l <logfile>  Log to <logfile> instead of default.

Mutually exclusive optional message types (instead of 'welcome' email):
    -u            Send 'update' email.
    -c            Send 'confirmation' email.
    -m <textfile> Massmail the contents of <textfile>.
";

%confirm_parameters = 
    (
     'subject',  'Confirming your MVHS Alumni Internet Directory entry',
     'log',      'confirm',
     'intro', 
"\nYou were sent this address verification message because it has been\n" .
"at least one year since we last confirmed your Directory entry.\n" .
"Please make sure your entry below is still current.  If everything\n" .
"you see below is correct, NO ACTION IS REQUIRED ON YOUR PART.\n",
     'textfile', '',
     'closing',  '',
     'sleep',    '0',
     );

%welcome_parameters = 
    (
     'subject',  'Welcome to the MVHS Alumni Internet Directory',
     'log',      'welcome',
     'intro',    '',
     'textfile', '',
     'closing',  "\nIf everything you see above is correct, you're all set.\n",
     'sleep',    '0',
     );

%update_parameters = 
    (
     'subject',  'Your MVHS Alumni Internet Directory entry is updated',
     'log',      'update',
     'intro',    '',
     'textfile', '',
     'closing',  "\nIf everything you see above is correct, you're all set.\n",
     'sleep',    '0',
     );

%massmail_parameters = 
    (
     'subject',  'MVHS Alumni Internet Directory',
     'log',      'massmail',
     'intro',    '',
     'textfile', '',
     'closing',
"\nIf you don't want to receive periodic mail like this, change the\n" .
"periodic mailings option on your update page to \"No email except for\n" .
"address verification.\"\n\n" .
"========================================================================\n\n",
     'sleep',    '10',
     );

&Getopts('hd:r:ucm:l:') || die "$usage\n";
$opt_h && die "$usage\n";
$opt_h = $opt_h;                # avoid warning
die "$pname: Too many mutually-exclusive options\n$usage\n"
    if (($opt_c ? 1 : 0) + ($opt_u ? 1 : 0) + ($opt_m ? 1 : 0)) > 1;
die "$pname: No target file specified\n$usage\n" unless $ARGV[0];
die "$pname: Option -m requires -r\n$usage\n" 
    if defined($opt_m) && !defined($opt_r);

%params = $opt_c ? %confirm_parameters : 
    $opt_u ? %update_parameters :
    $opt_m ? %massmail_parameters :
    %welcome_parameters;

$logfile = $opt_l ? $opt_l :
    &aid_config('aiddir') . "logs/" . $params{'log'} . '.log';

if ($opt_m) {
    open(TEXTFILE,$opt_m) || die "$pname: Can't open $opt_m: $!\n";
    while(<TEXTFILE>) { $params{'textfile'} .= $_; }
    close(TEXTFILE);
}

$params{'sleep'} = $opt_d if defined $opt_d;

open(LOG,">>$logfile") || die "$pname: Can't open $logfile: $!\n";
open(TARGET,$ARGV[0]) || die "$pname: Can't open $ARGV[0]: $!\n";

select(LOG); $| = 1; select(STDOUT);

$today = &ctime(time);
chop $today;

while(<TARGET>) {
    chop;
    %rec = &aid_split($_);
    next unless $rec{'valid'};
    next unless defined $rec{'email'} && $rec{'email'} ne '';
    next if $opt_m && $rec{'request'} != $opt_r;

    if ($opt_c) {
	next if defined $opt_r && $rec{'request'} != $opt_r;
	next unless &is_old($rec{'fresh'});
    }

    $message = &message_body($_) . $params{'textfile'} . &message_footer;

    &sendmail($rec{'email'},
	      &aid_config('admin_email'), &aid_config('admin_name'),
	      $params{'subject'}, $message);
    print LOG "[$today] - $rec{'id'} - $rec{'email'}\n";
 
    sleep $params{'sleep'} if $params{'sleep'} > 0;
}

exit(0);

sub message_body {
    local($[) = 0;
    local(%rec) = &aid_split($_[0]);
    local($denied) = $rec{'request'} == 0 ?
	"\nSince you asked not to receive Directory updates via email, " .
	"you will\nonly receive yearly address verification messages.\n" :
	 '';
    local($srv_prefix) = "http://" . &aid_config('master_srv');
    local($about) = &about_text(*rec,1,0,0);

    return "Dear $rec{'first'},

Your email address is listed in the Mountain View High School/Awalt High
School Alumni Internet Directory, located on the World Wide Web at:\n\n" .

$srv_prefix . &aid_config('master_path') . "
$params{'intro'}
The Directory (which produced this form letter) shows the following
information about you:

$about$denied
If you have received this email in error or do not want to be listed in
the Directory, please let me know and I will remove your entry.  If any
of the above information is incorrect or out of date, you can update
your entry online at:\n\n" .

$srv_prefix . &aid_config('cgi_path') . "/go?update=$rec{'id'}

If you don't have web access, just email me corrections and I'll be
happy to fix your entry.
$params{'closing'}";

}
