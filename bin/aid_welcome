#!/usr/local/bin/perl -w

#
# mails a welcome, update, confirm, or massmail message to members
# $Id: aid_welcome,v 5.38 2003/08/01 20:16:41 mradwin Exp mradwin $
#
#   Copyright (c) 2003  Michael J. Radwin
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#


$dirname = $0;
$dirname =~ s,/[^/]+$,,;
unshift(@INC, $dirname);

use aid_util;
use Getopt::Std;

use lib "/pub/m/r/mradwin/private/lib/perl5/site_perl";
use DB_File::Lock;
use Net::SMTP; 
use strict;

my $default_sleep = 0;
my $default_massmail_sleep = 5;

$0 =~ s,.*/,,;  # basename
my $usage = "usage: $0 [options] infile.db [target ids...]
    -h            Display usage information.
    -a            Mail all users in <infile.db> matching -r <reqtype>.
    -d <secs>     Sleep for <secs> seconds between sending e-mail
                  (default $default_massmail_sleep for -m, $default_sleep for all others).
    -r <reqtype>  Request type [0-4] (only valid with -m and -a).
    -l <logfile>  Log to <logfile> instead of default.

Mutually exclusive optional message types (instead of 'welcome' e-mail):
    -u            Send 'update' e-mail.
    -c            Send 'confirmation' e-mail.
    -m <textfile> Massmail the contents of <textfile>.
    -s <textfile> Special massmail contents of <textfile>.
";

my @args = @ARGV;

my(%opts);
getopts('hd:r:ucm:l:as:', \%opts) || die "$usage\n";
$opts{'h'} && die "$usage\n";
$a = $b = 0;                    # avoid warning
die "$0: Too many mutually-exclusive options\n$usage\n"
    if (($opts{'c'} ? 1 : 0) + ($opts{'u'} ? 1 : 0) + ($opts{'m'} ? 1 : 0) +
	($opts{'s'} ? 1 : 0)) > 1;
die "$0: No database file specified\n$usage\n" unless $ARGV[0];
die "$0: No target ids specified\n$usage\n" unless ($ARGV[1] || $opts{'a'});
die "$0: Option -m requires -r\n$usage\n" 
    if defined($opts{'m'}) && !defined($opts{'r'});
die "$0: Option -a requires -r\n$usage\n" 
    if defined($opts{'a'}) && !defined($opts{'r'});

my $directory_name = aid_util::config('short_school') . ' Alumni Internet Directory';

my %confirm_parameters = 
    (
     'subject'	=>	$directory_name,
     'log'	=>	'confirm',
     'sleep'	=>	$default_sleep,
     'listing'	=>	1,
     'intro'	=>
     "You were sent this address verification message because
it has been 6 months since we last confirmed your
Directory listing.  Please make sure your infromation
below is still current.  If everything you see below is
correct, NO ACTION IS REQUIRED ON YOUR PART.\n\n",

     'textfile'	=>	'',
     'closing'	=>	'',
     );

my %welcome_parameters = 
    (
     'subject'	=>	$directory_name,
     'log'	=>	'welcome',
     'sleep'	=>	$default_sleep,
     'listing'	=>	1,
     'intro'	=>
     "Welcome!  Your submission has been processed and you
are now listed on-line in the Directory.\n\n",
     'textfile'	=>	'',
     'closing'	=>
"
Otherwise, if everything you see above is correct,
you're all set.

Once again, welcome to the Alumni Internet Directory!
",
     );

my %update_parameters = 
    (
     'subject'	=>	$directory_name,
     'log'	=>	'update',
     'sleep'	=>	$default_sleep,
     'listing'	=>	1,
     'intro'	=>
     "Your contact information has been updated and the
changes are now visible on-line.\n\n",

     'textfile'	=>	'',
     'closing'	=>
"
Otherwise, if everything you see above is correct,
you're all set.
",
     );

my %massmail_parameters = 
    (
     'subject'	=>	$directory_name,
     'log'	=>	'massmail',
     'sleep'	=>	$default_massmail_sleep,
     'listing'	=>	1,
     'intro'	=>
     "Per your request, your quarterly digest of the
Directory appears at the end of this message.\n\n",
     'textfile'	=>	'',
     'closing'	=>
"
You also may change your quarterly digest preference
by visiting the above URL.

",
     );

my %special_parameters = 
    (
     'subject'	=>	$directory_name,
     'log'	=>	'special',
     'sleep'	=>	$default_massmail_sleep,
     'listing'	=>	0,
     'intro'	=>	'',
     'textfile'	=>	'',
     'closing'	=>	'',
     );

my %params = $opts{'c'} ? %confirm_parameters : 
    $opts{'u'} ? %update_parameters :
    $opts{'s'} ? %special_parameters :
    $opts{'m'} ? %massmail_parameters :
    %welcome_parameters;

my $logfile = $opts{'l'} ? $opts{'l'} :
    aid_util::config('aiddir') . "logs/" . $params{'log'} . '.log';

if ($opts{'s'} || $opts{'m'}) {
    my $f = $opts{'s'} || $opts{'m'};
    open(TEXTFILE,$f) || die "$0: Can't open $f: $!\n";
    while(<TEXTFILE>) { $params{'textfile'} .= $_; }
    close(TEXTFILE);
}

$params{'sleep'} = $opts{'d'} if defined $opts{'d'};

my $dbmfile = shift;
die "$0: $dbmfile: $!\n" unless -r $dbmfile;

open(LOG,">>$logfile") || die "$0: Can't open $logfile: $!\n";
select(LOG); $| = 1; select(STDOUT);

my $today = scalar(localtime(time));
chop $today;

print LOG "# [$today] - $0 ", join(' ', @args), "\n";

if ($opts{'a'})
{
    @ARGV = ();

    my(%DB);
    tie(%DB, 'DB_File::Lock', $dbmfile, O_RDONLY, 0444, $DB_HASH, 'read')
	or die "$dbmfile: $!\n";
    while (my($key,$val) = each(%DB))
    {
	if ($key =~ /^\d+$/)
	{
	    my %rec = aid_util::db_unpack_rec($key,$val);

	    next unless $rec{'v'};
	    next if $opts{'m'} && $rec{'q'} != $opts{'r'};

	    if ($opts{'c'})
	    {
		next unless $rec{'q'} == 0;
		next unless aid_util::is_old($rec{'f'});
	    }

	    push(@ARGV, $rec{'id'});
	}
    }

    @ARGV = sort { $a <=> $b } @ARGV;
}

my(%lm);
my($id);
foreach $id (@ARGV)
{
    unless ($id =~ /^\d+$/)
    {
	warn "$0: skipping target `$id' (invalid id)\n";
	next;
    }

    my(%DB);
    tie(%DB, 'DB_File::Lock', $dbmfile, O_RDONLY, 0444, $DB_HASH, 'read')
	or die "$dbmfile: $!\n";

    unless (defined $DB{$id})
    {
	warn "$0: skipping target `$id' (missing from $dbmfile)\n";
	untie(%DB);
	next;
    }

    my %rec = aid_util::db_unpack_rec($id,$DB{$id});

    untie(%DB);

    if (mail_user(\%rec))
    {
	$lm{$id} = time;
	sleep($params{'sleep'}) if $params{'sleep'} > 0;
    }
}

close(LOG);

#if (-w $dbmfile)
if (0)
{
    my(%DB);
    tie(%DB, 'DB_File::Lock', $dbmfile, O_CREAT|O_RDWR, 0644, $DB_HASH, 'write')
	or die "$dbmfile: $!\n";

    # sucessful mailing; update timestamp
    while (my($key,$val) = each(%lm))
    {
	my %rec = aid_util::db_unpack_rec($key,$DB{$key});
	$rec{'lm'} = $val;
	$DB{$rec{'id'}} = aid_util::db_pack_rec(\%rec);
    }

    untie(%DB);
}

exit(0);

sub mail_user
{
    my($recp) = @_;
    my($message,$name,@mailto);

    if ($recp->{'v'} == 0)
    {
	warn "$0: skipping target `$recp->{'id'}' (valid bit is off)\n";
	return 0;
    }

    unless (defined $recp->{'e'} && $recp->{'e'} ne '')
    {
	warn "$0: skipping target `$recp->{'id'}' (has no email address)\n";
	return 0;
    }

    if ($params{'listing'}) {
	$message = &message_body($recp);
    } else {
	$message = '';
    }

    $message .= $params{'textfile'} .
	"\n-- \n" . aid_util::config('short_school') . 
	" Alumni Internet Directory\n" .
	"http://" . aid_util::config('master_srv') . aid_util::config('master_path');

    $name  = $recp->{'gn'};
    $name .= " $recp->{'mi'}."
	if defined $recp->{'mi'} && $recp->{'mi'} ne '';
    $name .= " $recp->{'sn'}";
    $name .= " $recp->{'mn'}"
	if defined $recp->{'mn'} && $recp->{'mn'} ne '';
    $name =~ s/\"/\'/g;

    @mailto = ($name,$recp->{'e'});
    if ($opts{'u'} && defined $recp->{'eo'} &&
	$recp->{'eo'} ne '' && lc($recp->{'eo'}) ne lc($recp->{'e'}))
    {
	push(@mailto,$name,$recp->{'eo'});
    }

    my($i);
    my($status) = 0;

    for ($i = 0; $status == 0 && $i < 3; $i++)
    {
	my $time = time;
	$status = aid_util::sendmail(
	    aid_util::config('admin_email'),
	    aid_util::config('short_school') . ' Alumni Robot',
	    $params{'subject'} .
		' [' . aid_util::config('short_school') . '-' . $recp->{'id'} . ']',
		$message,@mailto);

	sleep(3) if $status == 0;

	print LOG $time, " - $recp->{'id'} - $recp->{'e'} - $status\n";

	print LOG $time, " - $recp->{'id'} - $recp->{'eo'} - $status - OLD\n"
	    if ($opts{'u'} && defined $recp->{'eo'} &&
		$recp->{'eo'} ne '' && lc($recp->{'eo'}) ne lc($recp->{'e'}));
    }

    $status;
} 

sub message_body
{
    my($recp) = @_;
    my $about = aid_util::about_text($recp,1,0,0);
    my $denied = $recp->{'q'} == 0 ?
	"
Since you asked not to receive quarterly Directory
digests via e-mail, you will only receive bi-yearly
address verification messages.
" : '';

    return aid_util::inorder_fullname($recp) . ",

$params{'intro'}You are listed in the Directory as follows:
-------------------------------------------------------
${about}-------------------------------------------------------
$denied
If any of the above information is incorrect or out
of date, please update your listing online at:

  http://" . aid_util::config('master_srv') . aid_util::config('go_cgi') . "/$recp->{'id'}

If you have received this e-mail in error, or you do
not want to be listed in the Directory, please let
me know and I will remove your listing.
$params{'closing'}";

}
