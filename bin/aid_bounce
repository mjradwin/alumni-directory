#!/usr/local/bin/perl -w

# $Id: aid_bounce,v 5.12 2001/11/01 20:08:28 mradwin Exp mradwin $
#
#   Copyright (c) 1995-1999  Michael John Radwin
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#

$dirname = $0;
$dirname =~ s,/[^/]+$,,;
unshift(@INC, $dirname);

use DB_File;
use Fcntl qw(:DEFAULT :flock);

use Getopt::Std;
require 'aid_util.pl';

$0 =~ s,.*/,,;  # basename
$usage = "usage: $0 [-hvud] [-f <log>] inoutfile.db [ids...]
    -h        Display usage information.
    -v        Invalidate ids (clear valid bit).
    -u        Revalidate ids (zero bounce field and set valid bit).
    -d        Display goners candidates.
    -f <log>  Set all freshness fields to value stored in <log> file
              unless an entry has non-zero bounce or freshness is
              later than time in <log> file.

Unless -d or -f is specified, sets the bounce/updated field for every id
or e-mail address listed on the command line.  The valid bit is untouched
unless -v or -u is specified.";

getopts('hvf:du') || die "$usage\n";
$opt_h && die "$usage\n";
$opt_h = $opt_h;                # avoid warning

die "$0: Too many mutually-exclusive options\n$usage\n"
    if (($opt_d ? 1 : 0) + ($opt_f ? 1 : 0) +
	($opt_v ? 1 : 0) + ($opt_u ? 1 : 0)) > 1;
die "$0: No database file specified\n$usage\n" unless $ARGV[0];
die "$0: No target ids specified\n$usage\n"
    unless ($opt_d || $opt_f || $ARGV[1]);

$dbmfile = shift;

if ($opt_d)
{
    die "$dbmfile: $!\n" unless -r $dbmfile;

    $mode = (stat($dbmfile))[2];
    chmod 0444, $dbmfile;

    my($db) = tie(%DB, 'DB_File', $dbmfile, O_RDONLY, 0444, $DB_File::DB_HASH)
	or die "$dbmfile: $!\n";
    my($fd) = $db->fd;
    open(DB_FH, "<&=$fd") || die "dup $!";
    unless (flock (DB_FH, LOCK_SH)) { die "flock: $!" }

    while (($key,$val) = each %DB)
    {
	next unless $key =~ /^\d+$/;
	%rec = &aid_db_unpack_rec($key,$val);

	next unless $rec{'v'};
	next if $rec{'b'} eq '0';

	print "$rec{'id'} - $rec{'e'} - bounce ", &aid_caldate($rec{'b'});
	print " - fresh ", &aid_caldate($rec{'f'})
	    if $rec{'f'} > $rec{'b'};
	print "\n";
    }

    flock(DB_FH, LOCK_UN);
    undef $db;
    untie(%DB);
    close(DB_FH);

    chmod $mode, $dbmfile;
    exit(0);
}
elsif ($opt_f)
{
    my($db) = tie(%DB, 'DB_File', $dbmfile, O_RDWR|O_CREAT, 0644,
		  $DB_File::DB_HASH)
	or die "$dbmfile: $!\n";
    my($fd) = $db->fd;
    open(DB_FH, "+<&=$fd") || die "dup $!";
    unless (flock (DB_FH, LOCK_EX)) { die "flock: $!" }

    @freshness = &time_log($opt_f);
    &freshen(@freshness);

    flock(DB_FH, LOCK_UN);
    undef $db;
    untie(%DBS);
    close(DB_FH);
}
else
{
    my($db) = tie(%DB, 'DB_File', $dbmfile, O_RDWR|O_CREAT, 0644,
		  $DB_File::DB_HASH)
	or die "$dbmfile: $!\n";
    my($fd) = $db->fd;
    open(DB_FH, "+<&=$fd") || die "dup $!";
    unless (flock (DB_FH, LOCK_EX)) { die "flock: $!" }

    &bounce_args(@ARGV);
    &aid_rebuild_secondary_keys(*DB,0);

    flock(DB_FH, LOCK_UN);
    undef $db;
    untie(%DBS);
    close(DB_FH);
}

exit(0);

# returns array[id] = time
sub time_log
{
    local($log) = @_;
    local($_,@log,*LOG);

    @log = ();
    open(LOG,$log) || die "error: can't open $log: $!\n";
    while(<LOG>)
    {
	if (/^(\d+) - (\d+) - /)
	{
	    $log[$2] = $1;
	}
    }
    close(LOG);

    @log;
}



# freshen the user only if the bounce was non-zero and their
# freshness is less than the parameter.
sub freshen
{
    local(@freshness) = @_;
    local($_);

    while (($key,$val) = each %DB)
    {
	next unless $key =~ /^\d+$/;
	%rec = &aid_db_unpack_rec($key,$val);

	next unless $rec{'v'} == 1 && defined $freshness[$rec{'id'}] &&
	    $rec{'b'} == 0 && $rec{'f'} < $freshness[$rec{'id'}];

	$rec{'f'} = $freshness[$rec{'id'}];
	$DB{$rec{'id'}} = &aid_db_pack_rec(*rec);
    }

    1;
}

# if the user has never bounced, set firstbounce to time.
# if the user has bounced, set freshness to time
sub bounce_args
{
    local(@args) = @_;
    local($time) = time;

    for (@args) {
	if (/^\d+$/) {
	    if (defined $DB{$_}) {
		%rec = &aid_db_unpack_rec($_,$DB{$_});
	    } else {
		warn "can't find id $_\n";
		next;
	    }
	} else {
	    $_ = "\L$_\E";
	    if (defined $DB{$_} && $DB{$_} ne '') {
		$_ = unpack("n*", $DB{$_});
		%rec = &aid_db_unpack_rec($_,$DB{$_});
	    } else {
		warn "can't find e-mail addr $_\n";
		next;
	    }
	}

	print "$rec{'id'} - $rec{'e'} - bounce ";
	if ($rec{'b'} == 0) {
	    print "(none)";
	} else {
	    print &aid_caldate($rec{'b'});
	}

	if ($opt_u)
	{
	    $rec{'b'} = 0;
	    $rec{'v'} = 1;
	    print " - clear";
	}
	else
	{
	    if ($rec{'b'} != 0)
	    {
		$rec{'f'} = $time;
	    }
	    else
	    {
		$rec{'b'} = $time;
	    }

	    if ($opt_v)
	    {
		$rec{'v'} = 0;
		$rec{'q'} = $aid_util::blank_entry{'q'};
	    }
	}
	print "\n";

	$DB{$rec{'id'}} = &aid_db_pack_rec(*rec);
    }

    1;
}

if ($^W && 0)
{
    %aid_util::blank_entry = ();
}
