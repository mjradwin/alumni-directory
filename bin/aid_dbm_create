#!/usr/local/bin/perl -w

# $Id: aid_dbm_create,v 6.5 2003/10/30 17:36:50 mradwin Exp mradwin $
#
# Copyright (c) 2003  Michael J. Radwin.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or
# without modification, are permitted provided that the following
# conditions are met:
#
#  * Redistributions of source code must retain the above
#    copyright notice, this list of conditions and the following
#    disclaimer.
#
#  * Redistributions in binary form must reproduce the above
#    copyright notice, this list of conditions and the following
#    disclaimer in the documentation and/or other materials
#    provided with the distribution.
#
#  * Neither the name of the High School Alumni Internet Directory
#    nor the names of its contributors may be used to endorse or
#    promote products derived from this software without specific
#    prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND
# CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
# CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
# NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
# OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

BEGIN {
  if ($0 =~ m,(.*[/\\]),) {
    unshift @INC, $1;
  } else {
    unshift @INC, '.';
  }
}
use lib "/home/mradwin/local/share/perl";

use DB_File::Lock;
use Config::IniFiles;
use Getopt::Std;
use aid_util;
use strict;

$0 =~ s,.*/,,;  # basename
my $usage = "usage: $0 [-hq] outfile.db infile.ini
    -h        Display usage information.
    -q        Quiet.
";

my(%opts);
getopts('hq', \%opts) || die "$usage\n";
$opts{'h'} && die "$usage\n";
defined $ARGV[0] || die "$usage\n";
defined $ARGV[1] || die "$usage\n";

my $dbmfile = shift;

my(%DB);
tie(%DB, 'DB_File::Lock', $dbmfile, O_CREAT|O_RDWR, 0644, $DB_HASH, 'write')
    or die "$dbmfile: $!\n";

unless ($opts{'q'})
{
    select(STDOUT); $| = 1;
}

my $infile = shift;

print STDOUT "$0: loading $infile..." unless $opts{'q'};
my $data = new Config::IniFiles(-file => $infile);
print STDOUT "done.\n" unless $opts{'q'};

print STDOUT "$0: writing primary data..." unless $opts{'q'};
if ($data)
{
    my $j = 0;
    foreach my $section ($data->Sections())
    {
	my %rec;

	foreach my $key (@aid_util::edit_field_names)
	{
	    my $val = $data->val($section, $key);
	    if (defined $val)
	    {
		$rec{$key} = $val;
	    }
	    else
	    {
		$rec{$key} = '';
	    }
	}

	my $note = $data->val($section, 'n');
	if (defined $note)
	{
	    $rec{'n'} = aid_util::url_unescape($note);
	}
	else
	{
	    $rec{'n'} = '';
	}

	if (! defined $rec{'id'})
	{
	    warn "$infile:$section: missing id\n";
	    next;
	}

	$rec{'a'} = aid_util::generate_alias(\%rec)
	    unless (defined $rec{'a'} && $rec{'a'} ne '');

	$DB{$rec{'id'}} = aid_util::db_pack_rec(\%rec);
	my $email = $rec{'e'};
	$DB{"\L$email\E"} = pack('n', $rec{'id'});

	print STDOUT "." if !$opts{'q'} && ($j++ % 20) == 0;
    }
}

print STDOUT "\n" unless $opts{'q'};

aid_util::rebuild_secondary_keys(\%DB,$opts{'q'},0,0);

untie(%DB);

print STDOUT "$0: build successful\n" unless $opts{'q'};
exit(0);
