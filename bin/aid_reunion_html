#!/usr/bin/perl -w

#
# generates reunion info page
#
# $Id: aid_reunion_html,v 7.3 2010/08/02 16:36:00 mradwin Exp mradwin $
#
# Copyright (c) 2006  Michael J. Radwin.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or
# without modification, are permitted provided that the following
# conditions are met:
#
#  * Redistributions of source code must retain the above
#    copyright notice, this list of conditions and the following
#    disclaimer.
#
#  * Redistributions in binary form must reproduce the above
#    copyright notice, this list of conditions and the following
#    disclaimer in the documentation and/or other materials
#    provided with the distribution.
#
#  * Neither the name of the High School Alumni Directory
#    nor the names of its contributors may be used to endorse or
#    promote products derived from this software without specific
#    prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND
# CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
# CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
# NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
# OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

BEGIN {
  if ($0 =~ m,(.*[/\\]),) {
    unshift @INC, $1;
  } else {
    unshift @INC, '.';
  }
}

use strict;
use Time::Local;
use Getopt::Std;
use aid_util;
use DBI ();
use Symbol ();

$0 =~ s,.*/,,;  # basename
my $usage = "usage: $0 [-h] outfile
    -h        Display usage information.
";

my %opts;
getopts("hv", \%opts) || die "$usage\n";
$opts{"h"} && die "$usage\n";
@ARGV == 1 || die "$usage\n";

my $dbname = aid_util::config("dbname");
my $dbhost = aid_util::config("dbhost");
my $dsn = "DBI:mysql:database=$dbname;host=$dbhost";
my $dbh = DBI->connect($dsn,
		       aid_util::config("dbuser"),
		       aid_util::config("dbpass"))
    or die $DBI::errstr;

my $outfile = shift;
my $FH = Symbol::gensym();
open($FH,">$outfile") || die "Can't open $outfile: $!\n";

my $time = time(); # TODO use real time
print $FH aid_util::common_html_hdr(11,'Reunion Information',0,$time);

my $admin = aid_util::config('admin_email');
$admin =~ s/@/\&\#64;/g;
$admin =~ s/\./\&\#46;/g;

print $FH <<EOHTML
<h2>Alumni: help keep this page up-to-date!</h2>

<p>If your class reunion isn\'t listed here, first try to contact your
Senior year class officers (see your yearbook if you don\'t remember who
they were).  Failing that, try the Activities Office of Mountain View
High School, at 650-940-4600.</p>

<p>If you know the details about a reunion that isn\'t listed on this
page, please send e-mail to <code><a
href="mailto:$admin">$admin</a></code>.</p>

<p>If you are a class officer and would like to send e-mail to members
of your graduating class, please contact
<code>$admin</code>
to get a list of people who have asked not to receive such mail.</p>
EOHTML
    ;

my %upcoming;
my %past;

my $sql = <<EOSQL
SELECT reunion_gradclass,
    reunion_date,
    reunion_text
FROM $dbname.aid_reunion
EOSQL
;

my $sth = $dbh->prepare($sql);
$sth->execute or die $sth->errstr;
while (my($class,$date,$html) = $sth->fetchrow_array)
{
    if (!$date)
    {
	my $val = "TBA\0" . $html;
	$upcoming{$class} = $val;
    }
    else
    {
	$date =~ s/-/\//g;
	my($year,$mon,$mday) = split(/\//, $date, 3);

	my $val = $date . "\0" . $html;
	my $t = Time::Local::timelocal(59,59,23,$mday,$mon-1,$year-1900);
	if ($t > time)
	{
	    $upcoming{$class} = $val;
	}
	else
	{
	    $past{$class} = $val;
	}
    }
}
$dbh->disconnect;

print $FH "<h2>Upcoming reunions</h2>\n";
aid_util::write_reunion_hash($FH, \%upcoming);

print $FH "<h2>Past reunions</h2>\n";
aid_util::write_reunion_hash($FH, \%past);

print $FH aid_util::common_html_ftr(11,$time);

close($FH);
#utime $time, $time, $outfile;

# touch timestamp
my $now = time;
my $ts = $outfile;
$ts =~ s,([^/]+)$,\.$1,;
open(TS,">$ts") && close(TS);
#utime $now, $now, $ts;

exit(0);
