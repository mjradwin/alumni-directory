#!/usr/local/bin/perl -w

# $Id: aid_edit_entry,v 6.5 2003/10/30 07:28:18 mradwin Exp mradwin $
#
# Copyright (c) 2003  Michael J. Radwin.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or
# without modification, are permitted provided that the following
# conditions are met:
#
#  * Redistributions of source code must retain the above
#    copyright notice, this list of conditions and the following
#    disclaimer.
#
#  * Redistributions in binary form must reproduce the above
#    copyright notice, this list of conditions and the following
#    disclaimer in the documentation and/or other materials
#    provided with the distribution.
#
#  * Neither the name of the High School Alumni Internet Directory
#    nor the names of its contributors may be used to endorse or
#    promote products derived from this software without specific
#    prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND
# CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
# CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
# NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
# OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

BEGIN {
  if ($0 =~ m,(.*[/\\]),) {
    unshift @INC, $1;
  } else {
    unshift @INC, '.';
  }
}
use lib "/home/mradwin/local/share/perl";

use DB_File::Lock;
use Getopt::Std;
use aid_util;

$0 =~ s,.*/,,;  # basename
my $usage = "usage: $0 [-hduf] infile.db userid
    -h        Display usage information.
    -d        Display debug diffs.
    -u        Don't set update time.
    -f        Force creation of non-consecutive entry.
";

getopts('hduf') || die "$usage\n";
$opt_h && die "$usage\n";
$opt_h = $opt_h;		# avoid warning
$opt_d = $opt_d;		# avoid warning
$opt_f = $opt_f;		# avoid warning
defined $ARGV[0] || die "$usage\n";
defined $ARGV[1] || die "$usage\n";

$dbmfile = shift;
die "$0: $dbmfile: $!\n" unless -r $dbmfile;
if (-w $dbmfile) {
    tie(%DB, 'DB_File::Lock', $dbmfile, O_CREAT|O_RDWR, 0644, $DB_HASH, 'write')
	or die "$dbmfile: $!\n";
    $mode = 0644;
} else {
    tie(%DB, 'DB_File::Lock', $dbmfile, O_RDONLY, 0444, $DB_HASH, 'read')
	or die "$dbmfile: $!\n";
    $mode = 0444;
}

$id = shift;
if ($id =~ /^\d+$/)
{
    if (defined $DB{$id} && $DB{$id} ne '')
    {
	%rec = aid_util::db_unpack_rec($id,$DB{$id});
    }
    elsif ($opt_f)
    {
	%rec = %aid_util::blank_entry;
	$rec{'c'} = $rec{'u'} = $rec{'f'} = time;
	$rec{'id'} = $id;
    }
    else
    {
	die "$0: no key $id in $dbmfile\n";
    }
}
elsif ($id =~ /\@/)
{
    $email = "\L$id\E";
    die "$0: no key \L$id\E in $dbmfile\n"
	unless defined $DB{$email} && $DB{$email} ne '';

    $id = unpack("n*", $DB{$email});
    die "$0: no key $id in $dbmfile\n"
	unless defined $DB{$id} && $DB{$id} ne '';

    %rec = aid_util::db_unpack_rec($id,$DB{$id});
}
else
{
    %rec = %aid_util::blank_entry;
    $rec{'c'} = $rec{'u'} = $rec{'f'} = time;
    if (! defined $DB{'_nextid'})
    {
	$DB{'_nextid'} = 1;
    }
    $id = $rec{'id'} = $DB{'_nextid'};
    # don't set nextid here; rebuild_secondary_keys will get it
}

$note_beg_str = '_AID_BEGIN_NOTE_';
$note_end_str = '_AID_END_NOTE_';
$tmpfile = '/tmp/aid_edit_entry.' . $$;

open(TMPFILE,">$tmpfile") || die "$tmpfile: $!\n";
print TMPFILE '# $Id: aid_edit_entry,v 6.5 2003/10/30 07:28:18 mradwin Exp mradwin $', "\n";
print TMPFILE "# record last touched: ",
 (($rec{'f'} > $rec{'b'}) ?
  scalar(localtime($rec{'f'})) :
  scalar(localtime($rec{'b'}))), "\n";

foreach (@aid_util::edit_field_names)
{
    print TMPFILE $_, '=', $rec{$_}, "\n";
}
my($note) = $rec{'n'};
while (chomp($note)) { } # remove trailing newline(s)
print TMPFILE $note_beg_str, "\n", $note, "\n", $note_end_str, "\n";
close(TMPFILE);

$editor = (defined $ENV{'VISUAL'} ? $ENV{'VISUAL'} : 
	   (defined $ENV{'EDITOR'} ? $ENV{'EDITOR'} : 'vi'));
system "$editor $tmpfile";
aid_util::die_if_failure();

%newrec = ();
open(TMPFILE,$tmpfile) || die "$tmpfile: $!\n";
unlink($tmpfile);
undef($note);
while(<TMPFILE>)
{
    if (/^$note_beg_str/o)
    {
	$note = '';
	while(<TMPFILE>)
	{
	    last if /^$note_end_str/o;
	    $note .= $_;
	}
	$note = '' if $note =~ /^\s*$/;
    }
    else
    {
	chop;
	next if /^#/;
	next if /^\s*$/;
	warn "$0: $tmpfile: bad line $_\n" unless /^.+=.*$/;
	($key,$val) = split(/=/, $_, 2);
	if ($key eq 'n')
	{
	    $note = aid_util::url_unescape($val);
	}
	else
	{
	    $newrec{$key} = $val;
	}
    }
}
close(TMPFILE);

# note and id are special cases
$newrec{'n'} = $note if defined $note;
$newrec{'id'} = $id unless defined $newrec{'id'};
$newrec{'a'} = aid_util::generate_alias(\%newrec)
    unless (defined $newrec{'a'} && $newrec{'a'} ne '');

if ($id != $newrec{'id'})
{
    if ($newrec{'id'} !~ /^\d+$/)
    {
	warn "$0: id=`$newrec{'id'}' not numeric, reverting to old id=$id\n";
	$newrec{'id'} = $id;
    }
    elsif (defined $DB{$newrec{'id'}})
    {
	warn "$0: id=$newrec{'id'} clobbers existing entry, reverting to old id=$id\n";
	$newrec{'id'} = $id;
    }
}

# assign the old value unless it was explicit in the edit
$changed = 0;
while(my($key,$val) = each(%rec))
{
    $newrec{$key} = $rec{$key} unless defined $newrec{$key};
    $changed = 1 if $newrec{$key} ne $rec{$key};
}

if ($mode == 0444)
{
    warn "$0: can't save changes ($dbmfile not writable)\n"
	if $changed == 1;
}
elsif ($changed == 1)
{
    if ($id != $newrec{'id'})
    {
	warn "$0: changed id from $id to $newrec{'id'}\n";
	delete $DB{$id};
    }

    $newrec{'u'} = $newrec{'f'} = time
	if (!$opt_u && $newrec{'u'} == $rec{'u'} && $newrec{'f'} == $rec{'f'});
    $DB{$newrec{'id'}} = aid_util::db_pack_rec(\%newrec);

    # if the id or email has changed, we need to redo the mapping
    if ($id != $newrec{'id'} || $rec{'e'} ne $newrec{'e'})
    {
	$email = $rec{'e'};
	delete $DB{"\L$email\E"};
	$email = $newrec{'e'};
	$DB{"\L$email\E"} = pack('n', $newrec{'id'});
    }

    my @diffs = aid_util::rebuild_secondary_keys(\%DB,0);

    if ($opt_d)
    {
	foreach (@diffs)
	{
	    print "key $_ changed.\n";
	}
    }

    warn "$0: update successful\n";
}

untie(%DB);
exit(0);

if ($^W && 0)
{
    %aid_util::blank_entry = ();
    $opt_u = 0;
}
