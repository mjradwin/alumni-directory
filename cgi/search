#!/usr/local/bin/perl -w

#
#     FILE: search.pl
#   AUTHOR: Michael J. Radwin
#    DESCR: Alumni Internet Directory CGI search
#      $Id: search,v 6.1 2003/08/25 06:01:35 mradwin Exp mradwin $
#
#   Copyright (c) 2003  Michael J. Radwin
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#

use FindBin;
use lib $FindBin::Bin;
use lib "/pub/m/r/mradwin/private/lib/perl5/site_perl";

use DB_File::Lock;
use CGI qw(-no_xhtml);
use CGI::Carp qw(fatalsToBrowser);
use aid_util;
use strict;

my $dbmfile = aid_util::config('dbmfile');
die "$dbmfile: $!\n" unless -r $dbmfile;

my(%DB);
tie(%DB, 'DB_File::Lock', $dbmfile, O_RDONLY, 0444, $DB_HASH, 'read')
    or die "$dbmfile: $!\n";

my %in;
my($q) = new CGI;
foreach my $key ($q->param())
{
    my($val) = $q->param($key);
    $val = '' unless defined $val;
    $in{$key} = $val;
}

if (!defined($in{'q'}) || $in{'q'} =~ /^[^\w]*$/)
{
    print "Content-Type: text/html\015\012\015\012";

    print aid_util::common_html_hdr(6, 'Search', 1);
    &print_search_form('', defined $in{'y'} ? $in{'y'} : '');
    print aid_util::common_html_ftr(6);

    untie(%DB);

    close(STDOUT);
    exit(0);
}

defined $DB{'_alpha'} || die "$dbmfile: missing _alpha\n";
my @alpha_ids = unpack("n*",$DB{'_alpha'});

my $query_save = $in{'q'};
$query_save =~ s/^\s*//;
$query_save =~ s/\s*$//;
my $query = $query_save;
$query =~ s/[^\w]/ /g; #'#
$query =~ s/\band\b//ig;
$query =~ s/\bor\b//ig;
my @search_terms = split(/\s+/, $query);

my %results;
foreach (@alpha_ids)
{
    my %rec = aid_util::db_unpack_rec($_,$DB{$_});
    my $fullname = aid_util::inorder_fullname(\%rec);
    my $key = "\L$rec{'sn'}\0$rec{'gn'}\0$rec{'mi'}\0$rec{'mn'}\0$rec{'yr'}\E\0$rec{'id'}";

    if (defined $in{'y'} && $in{'y'} ne '')
    {
	next if $in{'y'} eq 'other' && $rec{'yr'} =~ /^\d+$/;
	next unless ($in{'y'} eq $rec{'yr'}) || $in{'y'} eq 'other';
    }

    $results{$key} = 1;
    foreach my $term (@search_terms)
    {
	if ($fullname !~ /$term/i)
	{
	    delete $results{$key};
	    last;
	}
    }
}
undef @alpha_ids;

print "Content-Type: text/html\015\012\015\012";

my @results = sort keys %results;
undef %results;

$query_save = aid_util::html_entify_str($query_save);

if ($#results == -1)
{
    print aid_util::common_html_hdr(26, 'No matches', 1);
    print "<p>Sorry, no matches were found containing\n";
    print "<strong>$query_save</strong>";
    print "\nin <strong>Class of $in{'y'}</strong>"
	if defined $in{'y'} && $in{'y'} =~ /^\d+$/;
    print "\nin <strong>Faculty/Staff</strong>"
	if defined $in{'y'} && $in{'y'} eq 'other';
    print "\nin <strong>all graduating classes</strong>"
	if ! defined $in{'y'} || $in{'y'} eq '';
    print ".</p>\n\n";

    print "<p>You might try broadening your search.  Try just the first\n";
    print "or last name, or search \"all years\" instead of a particular\n";
    print "graduating class.  Or, just browse the\n";
    print "<a href=\"", aid_util::config('master_path'), "alpha/\">";
    print "Alphabetical</a> or\n";
    print "<a href=\"", aid_util::config('master_path'), "class/\">";
    print "Graduating Class</a>\nlistings.</p>\n\n";

    print "<p>For some other reasons why you might not find who you're\n";
    print "looking for, please see the\n";
    print "<a href=\"", aid_util::config('master_path');
    print "etc/faq.html#incomplete\">Frequently Asked Questions</a>.</p>\n\n";

    &print_search_form($query_save, defined $in{'y'} ? $in{'y'} : '');
    print aid_util::common_html_ftr(26);

    untie(%DB);

    close(STDOUT);
    exit(0);
}
else
{
    if ($#results == 0)
    {
	print aid_util::common_html_hdr(26, 'Found 1 match', 1);
    }
    else
    {
	print aid_util::common_html_hdr(26, 'Found ' . ($#results+1) . ' matches', 1);
    }

    print "\n<ul>\n";
    foreach (@results)
    {
	my(undef,undef,undef,undef,undef,$id) = split(/\0/);
	my %rec = aid_util::db_unpack_rec($id,$DB{$id});
	my $fullname = aid_util::inorder_fullname(\%rec);
	my($affil,undef) = aid_util::affiliate(\%rec,0);
	
	print "<li>";
	if (!$rec{'v'} || $in{'upd'})
	{
	    print "<a href=\"", aid_util::config('about_cgi'), 
	    "/", $rec{'id'}, "\">$fullname</a> $affil";
	}
	else
	{
	    print "<a href=\"", aid_util::about_path(\%rec), 
	    "\">$fullname</a> $affil";
	}

	if (! $rec{'v'})
	{
	    print " <em>(invalid e-mail address)</em>";
	}
	print "\n";
    }
    print "</ul>\n";
    &print_search_form($query_save, defined $in{'y'} ? $in{'y'} : '');
    print aid_util::common_html_ftr(26);
}

untie(%DB);

close(STDOUT);
exit(0);

sub print_search_form
{
    my($q,$y) = @_;

    defined $DB{'_years'} || die "$dbmfile: missing _years\n";
    my @years = unpack("n*",$DB{'_years'});
    push(@years, 'other');

    print "<center>\n",
      "<form method=\"get\" action=\"" . aid_util::config('search_cgi') . "\">\n",
      "<input type=\"text\" name=\"q\" id=\"q\" size=\"25\" value=\"$q\">\n",
      "<select name=\"y\" id=\"y\">\n";
    print "<option";
    print " selected=\"selected\"" if '' eq $y;
    print " value=\"\">all years</option>\n";
    foreach (@years)
    {
	print "<option";
	print " selected=\"selected\"" if $_ eq $y;
	print ">$_" unless $_ eq 'other';
	print " value=\"other\">Faculty/Staff" if $_ eq 'other';
	print "</option>\n";
    }
    print "</select>\n";
    print "<input type=\"submit\" value=\"Search\">",
      "\n</form>\n</center>\n";

    1;
}
