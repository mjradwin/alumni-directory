#!/usr/local/bin/perl -w                                                                                                                                                                  
BEGIN {
    if ($0 =~ m,(.*[/\\]),) {
	unshift @INC, $1;
    } else {
	unshift @INC, ".";
    }
}

use strict;
use CGI qw(-no_xhtml);
use CGI::Carp qw(fatalsToBrowser);
use Email::Valid;
use aid_util;
use aid_submit;
use DBI ();

# configuration
my $rcsid = '$Id$'; #'fnt

my $q = new CGI;
my $id = $q->param("id");

if (!defined $id || $id !~ /^\d+$/) {
    aid_util::cgi_die("Form Data Missing",
		      "The required form data was not present or something\n" .
		      "is awry.");
}

my $dbh = aid_util::db_connect();
$dbh || aid_util::cgi_die("Database Failure", $DBI::errstr);

my $MDB = aid_util::load_records($dbh, "AND a.alumnus_id = $id");
if (!defined $MDB->{$id}) {
    $dbh->disconnect;
    aid_util::cgi_die("Alumnus id $id Not Found",
		      "Alumnus id $id Not Found");
}

my $randkey = create_key();
my $rec = $MDB->{$id};
staging_store_rec($dbh, $rec, $randkey);

my($return_path,$from,$subject,$body,@recip) =
    aid_util::verification_message($randkey, $rec);

my $status = 0;
for (my $i = 0; $status == 0 && $i < 3; $i++) {
    $status = aid_util::sendmail_v2($return_path,$from,$subject,$body,@recip);
}

aid_util::cgi_die("ERROR: Unable to send confirmation e-mail",
		  "We're sorry, we were unable to send e-mail to\n" .
		  "<b>$rec->{'e'}</b>.<p>This may be a problem with our\n" .
		  "server or perhaps with your e-mail address.\n" .
		  "Please try again in a few minutes.<p>If you continue\n" .
		  "to get this error, please send e-mail to <a\n" .
		  "href=\"mailto:" . aid_util::config("admin_email") .
		  "\">" . aid_util::config("admin_email") . "</a>\n" .
		  "with error code <b>$randkey</b>.")
    if $status == 0;

print "Content-Type: text/html\015\012\015\012";
print aid_util::common_html_hdr(20,"Verify e-mail address to delete account",1);

print "
<p>Our e-mail robot (<tt>", aid_util::config("smtp_user"), "</tt>)
just sent a verification message to
<b>$rec->{'e'}</b>.
It may take 2 or 3 minutes for it to arrive.</p>

<p>Please check your e-mail and find the
verification URL that was sent to you by the ",
aid_util::config("short_school"), "
Alumni Robot and click on it to delete your account.</p>
";

print aid_util::common_html_ftr(20);

close(STDOUT);
$dbh->disconnect;
exit(0);

sub create_key
{
    my $now = time();

    my $rand = pack("N", $now);

    if ($ENV{"REMOTE_ADDR"}) {
	$rand .= pack("CCCC", split(/\./, $ENV{"REMOTE_ADDR"}));
    }

    $rand .= pack("N", int(rand(0xFFFFFFFF)));

    unpack("H*", $rand);
}

sub staging_store_rec
{
    my($dbh,$rec,$randkey) = @_;

    my $sql = "INSERT INTO aid_verify"
	. " (verify_code,verify_staging_id,verify_alumnus_id,verify_created)"
	. " VALUES (?,?,?,NOW())";
    my $sth = $dbh->prepare($sql);
    $sth->execute($randkey, $rec->{"id"}, $rec->{"id"})
	or aid_util::cgi_die("Database Error", $sth->errstr);
}

