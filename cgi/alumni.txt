#!/usr/local/bin/perl -w

#
#     FILE: alumni.txt.pl
#   AUTHOR: Michael J. Radwin
#    DESCR: Address Book/Nicknames Downloading module
#      $Id: alumni.txt,v 5.13 2003/05/15 00:20:18 mradwin Exp mradwin $
#
#   Copyright (c) 2003  Michael J. Radwin
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#

use FindBin;
use lib $FindBin::Bin;
use lib "/pub/m/r/mradwin/private/lib/perl5/site_perl";

use DB_File::Lock;
use CGI;
use CGI::Carp qw(fatalsToBrowser);
use aid_util;

my $dbmfile = aid_util::config('dbmfile');
die "$dbmfile: $!\n" unless -r $dbmfile;

CGI::ReadParse();
if (! keys %in)
{
    print "Status: 400 Bad Request\015\012";
    aid_util::cgi_die("Bad Download Request",
	      "The download request could not be processed.\n" .
	      "Please <a href=\"" . aid_util::config('master_path') .
	      "download/\">try again</a>.");
}

$plusminus_p = defined $in{'plusminus'} && 
    ($in{'plusminus'} eq 'on' || $in{'plusminus'} == 1) ? 1 : 0;

if (! &is_email_okay($in{'email'}))
{
    print "Content-Type: text/html\015\012\015\012";
    print aid_util::common_html_hdr(-1, (($in{'email'} =~ /^\s*$/) ? 'Blank' :
				    'Invalid') . ' E-mail Address',1);
    print "\n";
    print "<p>You must submit a valid, non-blank e-mail address to ";
    print "download address books.\n";
    print "<p>Please <a href=\"" . aid_util::config('master_path') .
	"download/\">try again</a>.</p>\n";
    print aid_util::common_html_ftr(-1);
}
elsif (defined $in{'format'} && defined $in{'year'})
{
    &send_address_book($in{'format'},$in{'year'},$plusminus_p);
}
else
{
    print "Status: 400 Bad Request\015\012";
    aid_util::cgi_die("Bad Download Request",
	      "The download request could not be processed.\n" .
	      "Please <a href=\"" . aid_util::config('master_path') .
	      "download/\">try again</a>.");
}

close(STDOUT);
exit(0);


sub send_address_book {
    my($format,$year,$plusminus_p) = @_;
    my(@class_ids);
    my($minyr,$maxyr);
    my($subtype) = 'plain';
    my($filename) = aid_util::config('short_school');

    $filename =~ s/[^\w]/_/g;
    $filename = "\L$filename";
    $filename .= '_alumni_' . $year;

    if ($format eq 'o') {
	$subtype = 'x-csv';
	$filename .= '.csv';
    } elsif ($format eq 'v') {
	$subtype = 'directory';
	$filename .= '.vcf';
    } elsif ($format eq 'l') {
	$subtype = 'ldif';
	$filename .= '.ldif';
    } elsif ($format eq 'n') {
	$subtype = 'html;charset=ISO-8859-1';
	$filename .= '.html';
    } else {
	$filename .= '.txt';
    }

    if ($plusminus_p && $year =~ /^\d{4}$/) {
	$minyr = $year - 3;
	$maxyr = $year + 3;
    } else {
	$minyr = $maxyr = $year;
    }

    my(%DB);
    tie(%DB, 'DB_File::Lock', $dbmfile, O_RDONLY, 0444, $DB_HASH, 'read')
	or die "$dbmfile: $!\n";

    defined $DB{'_class'} || die "$dbmfile: missing _class\n";

    @class_ids = unpack("n*",$DB{'_class'});
    
    print "Content-Disposition: attachment; ",
	"filename=$filename\015\012";
    print "Content-Type: text/$subtype; ",
	"filename=\"$filename\"\015\012\015\012";
    open($STDOUT,">-") || die;
    aid_util::book_write_prefix($STDOUT,$format);
    foreach (@class_ids)
    {
	my %rec = aid_util::db_unpack_rec($_,$DB{$_});

	next unless $rec{'v'};
	if ($year =~ /^\d{4}$/)
	{
	    next unless ($rec{'yr'} =~ /^\d{4}$/ &&
			 $rec{'yr'} <= $maxyr && $rec{'yr'} >= $minyr);
	}
	else
	{
	    next if $rec{'yr'} =~ /^\d{4}$/;
	}

	aid_util::book_write_entry($STDOUT,$format,\%rec);
    }

    untie(%DB);

    aid_util::book_write_suffix($STDOUT,$format);
    close($STDOUT);
}

sub is_email_okay
{
    local($_) = @_;

    if (/^\s*[^\@\s\(\)\"]+\@([^\.\s\(\)\"]+\.)+\w\w\w?\s*$/)
    {
	1;
    }
    else
    {
	0;
    }
}

# avoid stupid warnings
if ($^W && 0)
{
    %in = ();
}

1;
